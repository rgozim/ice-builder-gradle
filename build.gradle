// **********************************************************************
//
// Copyright (c) 2014-present ZeroC, Inc. All rights reserved.
//
// **********************************************************************


apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-io:commons-io:2.6'

    testCompile 'junit:junit:4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
}

sourceSets {
    main {
        groovy {
            srcDir "src/main/groovy"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
}

version  = "1.5.0-SNAPSHOT"
group = "com.zeroc.gradle.ice-builder"

gradlePlugin {
    plugins {
        IcePlugin {
            id = 'com.zeroc.gradle.ice-builder.slice'
            implementationClass = 'com.zeroc.gradle.icebuilder.slice.SlicePlugin'
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv('ARTIFACTORY_USER') ?: "${findProperty('artifactory_user')}"
                password = System.getenv('ARTIFACTORY_PASSWORD') ?: "${findProperty('artifactory_password')}"
            }
            if (version.endsWith('SNAPSHOT')) {
                url "https://artifacts.openmicroscopy.org/artifactory/ome.snapshots"
            } else {
                url "https://artifacts.openmicroscopy.org/artifactory/ome.staging"
            }
        }
    }
}
